root = item* ;

item = binding ;

binding = 'let' NAME '=' expr ;

expr = '(' ')'                 (* Unit *)
     | NUMBER                  (* Int *)
     | 'true' | 'false'        (* Bool *)
     | STRING                  (* String *)
     | NAME                    (* Variable *)
     | let                     (* Let binding *)
     | lambda                  (* Lambda *)
     | if                      (* If-else *)
     | expr expr               (* Application
                                  Does not permit trailing let bindings, lambdas,
                                  or if expressions without parens. *)
     | '(' expr ')'            (* Syntactic grouping *)
     ;

let = 'let' NAME '=' expr 'in' expr ;

lambda = 'fun' NAME '->' expr ;

if = 'if' expr 'then' expr 'else' expr ;
